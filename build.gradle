/*
	Created by Davood Mazinanian 21 Nov 2015
*/

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'

sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'ca.concordia.cssanalyser.app.CSSAnalyserCLI'

repositories {
	mavenCentral()
}

dependencies {
	compile "org.choco-solver:choco-solver:${choco_solver_version}"
	compile "com.crawljax:crawljax-core:${crawljax_version}"
	compile "com.crawljax.plugins:crawloverview-plugin:${crawljax_crawloverview_plugin_version}"
	compile "com.github.sommeri:less4j:${less4j_version}"
	compile "org.w3c.css:sac:${sac_version}"
	testCompile "junit:junit:${junit_version}"
}

jar {
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.MF'

    manifest {
        attributes 'Main-Class': mainClassName,
                   "Class-Path": configurations.compile.collect { "libs/${it.getName()}" }.join(' ')
    }
}

test {
     include "ca/concordia/cssanalyser/testrunner/AllTestsSuite.class"
}

task copyToLibs(type: Copy) {
    into "$buildDir/libs/libs"
    from configurations.runtime
}

assemble.dependsOn copyToLibs